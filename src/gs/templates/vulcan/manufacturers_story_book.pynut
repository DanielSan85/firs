class VulcanManufacturersStoryBook {

    function Init() {
        // note that this creates a table in root
        // not designed to return a class instance
        // vulcan_manufacturers_story_pages is a singleton, and allegedly a table with slots is more efficient for that case than a singleton class instance
        ::vulcan_manufacturers_story_pages <- {};
        VulcanManufacturersStoryBook.AddManfuacturersStoryPages();
    }

    function AddManfuacturersStoryPages() {
        local asyncmode = GSAsyncMode(true);
        foreach (manufacturer_id, manufacturer in ::persistent_storage["manufacturers"]) {
            vulcan_manufacturers_story_pages[manufacturer_id] <- VulcanManufacturerStoryPage(manufacturer_id, manufacturer);
        }
        local asyncmode = null;
    }
}

class VulcanManufacturerStoryPage {

    // keep a copy of the story page (for convenience)
    story_page = null
    page_elements = null

    constructor(manufacturer_id, manufacturer) {
        this.story_page = GSStoryPage.New(GSCompany.COMPANY_INVALID, manufacturer["name"] + " (" + manufacturer["category"] + ")");
        this.CreatePageElements(manufacturer_id, manufacturer);
    }

    function ListIndustriesOwned(manufacturer, extra_params) {
        local result = [];
        foreach (industry in VulcanIndustryOwnership.ListIndustries("manufacturer", extra_params[0])) {
            result.append(GSIndustry.GetName(industry));
        }
        return GSText(GSText.STR_MANUFACTURERS_STORY_PAGE_INDUSTRIES_OWNED, Repr(result));
    }

    function CreatePageElements(manufacturer_id, manufacturer) {
        // define the elements
        this.page_elements = [];

        this.page_elements.append({
            reference = null,
            // when only a GSText is needed, just wrap it in an anonymous function
            text_method = function(vulcan_town, extra_params) {
                return GSText(GSText.STR_MANUFACTURERS_STORY_PAGE_NAME, extra_params[0])
            },
            extra_params = [manufacturer["name"]]
        });
        this.page_elements.append({
            reference = null,
            // when only a GSText is needed, just wrap it in an anonymous function
            text_method = function(vulcan_town, extra_params) {
                return GSText(GSText.STR_MANUFACTURERS_STORY_PAGE_CATEGORY, extra_params[0])
            },
            extra_params = [manufacturer["category"]]
        });
        this.page_elements.append({
            reference = null,
            // when only a GSText is needed, just wrap it in an anonymous function
            text_method = function(vulcan_town, extra_params) {
                return GSText(GSText.STR_MANUFACTURERS_STORY_PAGE_HQ_TOWN, extra_params[0])
            },
            extra_params = [manufacturer["hq_town"]]
        });
        this.page_elements.append({
            reference = null,
            // when only a GSText is needed, just wrap it in an anonymous function
            text_method = function(vulcan_town, extra_params) {
                return GSText(GSText.STR_MANUFACTURERS_STORY_PAGE_VALID_TOWNS, extra_params[0])
            },
            extra_params = [Repr(manufacturer["valid_towns"])]
        });
        this.page_elements.append({
            reference = null,
            text_method = ListIndustriesOwned,
            extra_params = [manufacturer_id]
        });

        // add the elements
        foreach (page_element in this.page_elements) {
            local extra_params = null;
            if ("extra_params" in page_element) {
                extra_params = page_element.extra_params;
            }
            page_element.reference = GSStoryPage.NewElement(this.story_page, GSStoryPage.SPET_TEXT, 1, page_element.text_method(manufacturer, extra_params));
        }
    }

    function UpdatePage(manufacturer_id, manufacturer) {
        // remove all elements and recreate them, it's the safest way to accommodate changing entities such as industries which can open / close
        foreach (page_element in this.page_elements) {
            GSStoryPage.RemoveElement(page_element.reference);
        }
        this.CreatePageElements(manufacturer_id, manufacturer);
        GSStoryPage.SetDate(this.story_page, GSDate.GetCurrentDate());
    }
}
